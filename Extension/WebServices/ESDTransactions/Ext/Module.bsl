
Функция LoadTransactions(TableESD, DateLoad)
	
	//Инициализация и создание выходного значения
	ResultOfOperationТип = ФабрикаXDTO.Тип("ESDTransactions", "ResultOfOperation" );
	ResultOfOperation = ФабрикаXDTO.Создать(ResultOfOperationТип);
    ResultOfOperation.Success = Ложь;
	ResultOfOperation.Comments = "";
	
	
	Если НЕ ЗначениеЗаполнено(DateLoad) Тогда
		ResultOfOperation.Comments = "Ошибка: Дата загрузки пустая.";
		Возврат ResultOfOperation;
	КонецЕсли;
	
	Если TableESD.Element.Количество() = 0 Тогда 	
		ResultOfOperation.Comments = "Ошибка: Пустая входящая TableESD.";
		Возврат ResultOfOperation;
	КонецЕсли;

	// очистка записей в загружаемой дате
	НаборЗаписей = РегистрыСведений.ТранзакцииESD.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Date.Установить(DateLoad);
	Попытка
		НаборЗаписей.Записать();
	Исключение
		ЗаписьЖурналаРегистрации("ЗагрузкаТранзакцийESD.ОшибкаОчисткиТранзакций",
			УровеньЖурналаРегистрации.Ошибка, Метаданные.РегистрыСведений.ТранзакцииESD,, ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			
		ResultOfOperation.Comments = "Ошибка записи переданных транзакций.";
		Возврат ResultOfOperation;
	КонецПопытки;
	
	Для Каждого Транзакция Из TableESD.Element Цикл
		
		НаборЗаписей = РегистрыСведений.ТранзакцииESD.СоздатьНаборЗаписей();
		
		НаборЗаписей.Отбор.Date.Установить(DateLoad);
		НаборЗаписей.Отбор.AgreementScId.Установить(Транзакция.AgreementScId);
		НаборЗаписей.Отбор.NomenclatureScId.Установить(Транзакция.NomenclatureScId);
		НаборЗаписей.Отбор.ServerTxId.Установить(Транзакция.ServerTxId);
		НаборЗаписей.Отбор.TxRef.Установить(Транзакция.TxRef);
		НаборЗаписей.Отбор.Manufacture.Установить(Транзакция.Manufacture);
		
		Запись = НаборЗаписей.Добавить();
		ЗаполнитьЗначенияСвойств(Запись, Транзакция);
		Запись.Date = DateLoad;
		
		Попытка
			НаборЗаписей.Записать();
		Исключение
			ЗаписьЖурналаРегистрации("ЗагрузкаТранзакцийESD.ОшибкаЗагрузкиТранзакций",
				УровеньЖурналаРегистрации.Ошибка, Метаданные.РегистрыСведений.ТранзакцииESD,, ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
				
			ResultOfOperation.Comments = "Ошибка записи переданных транзакций.";
			Возврат ResultOfOperation;
		КонецПопытки;
		
		Для Каждого Серия Из Транзакция.Serials Цикл
			
			НаборЗаписей = РегистрыСведений.СерииESD.СоздатьНаборЗаписей();
			
			НаборЗаписей.Отбор.ServerTxId.Установить(Транзакция.ServerTxId);
			НаборЗаписей.Отбор.SerialSetScId.Установить(Серия.SerialSetScId);
			НаборЗаписей.Отбор.SerialId.Установить(Серия.SerialId);
			НаборЗаписей.Отбор.TransactionKind.Установить(Серия.TransactionKind);
		
			Запись = НаборЗаписей.Добавить();
			ЗаполнитьЗначенияСвойств(Запись, Серия);
			Запись.ServerTxId = Транзакция.ServerTxId;
			
			Попытка
				НаборЗаписей.Записать();
			Исключение
				ЗаписьЖурналаРегистрации("ЗагрузкаТранзакцийESD.ОшибкаЗагрузкиСерии",
					УровеньЖурналаРегистрации.Ошибка, Метаданные.РегистрыСведений.СерииESD,, ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
					
				ResultOfOperation.Comments = "Ошибка записи переданных транзакций.";
				Возврат ResultOfOperation;
			КонецПопытки;
		КонецЦикла;
		
	КонецЦикла;
	
    ResultOfOperation.Success = Истина;
	
	Возврат ResultOfOperation;	
	
КонецФункции

Функция LoadAdjustments(TableAdjustments)
	
	//Инициализация и создание выходного значения
	ResultOfOperationТип = ФабрикаXDTO.Тип("ESDTransactions", "ResultOfOperation" );
	ResultOfOperation = ФабрикаXDTO.Создать(ResultOfOperationТип);
    ResultOfOperation.Success = Ложь;
	ResultOfOperation.Comments = "";
	
	Если TableAdjustments.Elements.Количество() = 0 Тогда 	
		ResultOfOperation.Comments = "Ошибка: Пустая входящая TableAdjustments.";
		Возврат ResultOfOperation;
	КонецЕсли;

	Для Каждого Транзакция Из TableAdjustments.Elements Цикл
			
		НаборЗаписей = РегистрыСведений.КорректировкиESD.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.ServerTxId.Установить(Транзакция.ServerTxId);
	
		Запись = НаборЗаписей.Добавить();
		ЗаполнитьЗначенияСвойств(Запись, Транзакция);
		
		Попытка
			НаборЗаписей.Записать();
		Исключение
			ЗаписьЖурналаРегистрации("ЗагрузкаТранзакцийESD.ОшибкаЗагрузкиКорректировок",
				УровеньЖурналаРегистрации.Ошибка, Метаданные.РегистрыСведений.КорректировкиESD,, ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
				
			ResultOfOperation.Comments = "Ошибка записи переданных корректировок.";
			Возврат ResultOfOperation;
		КонецПопытки;
		
	КонецЦикла;
	
    ResultOfOperation.Success = Истина;
	Возврат ResultOfOperation;	
	
КонецФункции





